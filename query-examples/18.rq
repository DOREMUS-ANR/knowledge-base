PREFIX mus: <http://data.doremus.org/ontology#>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX efrbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

# [en] Give me the list of concerts given at the Philharmonie de Paris in which the orchestra is directed by the solo violin
# [fr] Donne-moi la liste des concerts donnés à la Philharmonie de Paris dans lesquels l’orchestre est dirigé par le violon solo

SELECT DISTINCT ?concert ?conductor ?note
WHERE {
  ?concert ecrm:P9_consists_of / ecrm:P9_consists_of* ?activityDir ;
      ecrm:P9_consists_of / ecrm:P9_consists_of* ?activityMus ;
      ecrm:P7_took_place_at <http://data.doremus.org/place/a12b6211-da7f-34dc-ad6d-6b6467608772> .

  ?activityDir ecrm:P14_carried_out_by ?conductor ;
    mus:U31_had_function / skos:broader* <http://data.doremus.org/vocabulary/function/conductor> .

  ?activityMus ecrm:P14_carried_out_by ?conductor ;
    mus:U1_used_medium_of_performance / skos:exactMatch* <http://data.doremus.org/vocabulary/iaml/mop/svl> .

  OPTIONAL { ?concert mus:U205_has_cast_detail ?note }
} LIMIT 100
