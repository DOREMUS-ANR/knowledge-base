PREFIX mus: <http://data.doremus.org/ontology#>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX efrbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

# [en] Give me all the recordings of the _Catalogue Aria_ (isolated air or in a recording of the opera)
# [fr] Donne moi tous les enregistrements de l'_Air du catalogue_ (air isolé ou dans un enregistrement de l’opéra)

SELECT DISTINCT ?rec
WHERE {
  ?rec mus:U51_is_partial_or_full_recording_of ?perf.

  ?perf rdfs:label ?label ;
         mus:U54_is_performed_expression_of	?work .

  ?work rdfs:label ?title.

  ?expCreation efrbroo:R17_created ?work ;
          ecrm:P9_consists_of / ecrm:P14_carried_out_by <http://data.doremus.org/artist/4802a043-23bb-3b8d-a443-4a3bd22ccc63> .

  FILTER (contains(str(?label), "Air du catalogue"))
  FILTER (contains(str(?title), "Don Giovanni"))
}

# Note: Catalogue Aria is contained in Don Giovanni
