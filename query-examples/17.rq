PREFIX mus: <http://data.doremus.org/ontology#>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX efrbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

# [en] Give me the list of concerts of the Orchestre National de France in which the conductor is also instrumental soloist (in the same concert)
# [fr] Donne-moi la liste des concerts de l'Orchestre National de France dans lesquels le chef d'orchestre est également soliste instrumental (dans le même concert)


SELECT DISTINCT ?performance, ?conductor
WHERE {
  ?performance a efrbroo:F31_Performance ;
    efrbroo:R25_performed / ecrm:P165_incorporates ?expression ;
    ecrm:P9_consists_of ?activityDir ;
    ecrm:P9_consists_of ?activityMus ;
    ecrm:P9_consists_of / ecrm:P14_carried_out_by ?orchestra.
  
  ?activityDir ecrm:P14_carried_out_by ?conductor ;
    mus:U35_foresees_function_of_type "conducteur"@fr .
    
  ?activityMus ecrm:P14_carried_out_by ?conductor ;
    mus:U1_used_medium_of_performance ?mop .


  ?expression a efrbroo:F22_Self-Contained_Expression ;
          mus:U70_has_title ?title ;
          mus:U13_has_casting / mus:U23_has_casting_detail ?castingDet .

  ?castingDet mus:U2_foresees_use_of_medium_of_performance_of_type ?mop;
              mus:U36_foresees_responsibility_of_type "soloist"@fr .
    
  FILTER ( contains(lcase(str(?orchestra)), "orchestre national de france") )
}

# Note: there are no data about it

# Note: this query will be improved by the creation of the controlled vocabulary of functions