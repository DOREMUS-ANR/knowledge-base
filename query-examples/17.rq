PREFIX mus: <http://data.doremus.org/ontology#>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX efrbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

# [en] Give me the list of concerts of the Orchestre National de France in which the conductor is also instrumental soloist (in the same concert)
# [fr] Donne-moi la liste des concerts de l'Orchestre National de France dans lesquels le chef d'orchestre est également soliste instrumental (dans le même concert)


SELECT DISTINCT ?concert ?conductor
WHERE {
  ?concert ecrm:P9_consists_of / ecrm:P9_consists_of* ?activityDir ;
      ecrm:P9_consists_of / ecrm:P9_consists_of* ?activityMus ;
      ecrm:P9_consists_of / ecrm:P9_consists_of* / ecrm:P14_carried_out_by / rdfs:label ?orchestra.

  ?activityDir ecrm:P14_carried_out_by ?conductor ;
    mus:U31_had_function / skos:broader* <http://data.doremus.org/vocabulary/function/conductor> .

  ?activityMus ecrm:P14_carried_out_by ?conductor ;
    mus:U1_used_medium_of_performance ?mop ;
    mus:U32_had_responsibility "soloist"@fr .

  FILTER ( contains(lcase(str(?orchestra)), "orchestre national de france") )
}

# Note: there are no data about it

# Note: this query will be improved by the creation of the controlled vocabulary of resposibilities
